apply plugin: 'com.android.application'

task cleanResGen(type: Delete) {
    mkdir 'src/main/res-gen/xml/'
    mkdir 'src/main/res-gen/drawable/'
    mkdir 'src/main/res-gen/raw/'

    delete fileTree('src/main/res-gen/xml/*.xml') {
        include '*.xml'
    }
    delete fileTree('src/main/res-gen/drawable/*')
    delete fileTree('src/main/res-gen/raw/*')
}

task genResXml(type: Copy) {
    from 'src/main/res-dev/tmx'
    into 'src/main/res-gen/xml'
    rename '(.*).tmx', '$1.xml'
}

task genResDrawable(type: Copy) {
    from 'src/main/res-dev/drawable'
    into 'src/main/res-gen/drawable'
}

task genResRaw(type: Copy) {
    from 'src/main/res-dev/raw'
    into 'src/main/res-gen/raw'
}

genResXml.dependsOn cleanResGen
genResDrawable.dependsOn cleanResGen
genResRaw.dependsOn cleanResGen

android {
    signingConfigs {
    }
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "com.gpl.rpg.AndorsTrail"
        minSdkVersion 4
        targetSdkVersion 28
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    sourceSets {
        main {
            res.srcDirs = ['src/main/res', 'src/main/res-gen']
        }
    }
}


dependencies {
    implementation 'com.android.support:support-v4:28.0.0'
    api 'com.android.support:support-v4:28.0.0'
}

preBuild.dependsOn genResXml
preBuild.dependsOn genResDrawable
preBuild.dependsOn genResRaw
